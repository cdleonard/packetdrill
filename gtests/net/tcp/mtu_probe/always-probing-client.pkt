//
// With tcp_mtu_probing=2 mtu probing should be always enabled and
// use an initial MSS of tcp_base_mss
// 

// enforce strict segment checking (and turn off gso) to measure probe length
--strict_segments

`../common/defaults.sh
 ../common/set_sysctls.py \
	/proc/sys/net/ipv4/tcp_mtu_probing=2 \
	/proc/sys/net/ipv4/tcp_timestamps=0 \
	/proc/sys/net/ipv4/tcp_base_mss=1000
 ip link set dev tun0 mtu 9040
 ethtool -K tun0 gso off tso off
`

    0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
   +0 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
  +.08...0.180 connect(3, ..., ...) = 0
   +0 > S  0:0(0) win 65535 <mss 9000,nop,nop,sackOK,nop,wscale 8>
  +.1 < S. 0:0(0) ack 1 win 50000 <mss 9000>
   +0 > .  1:1(0) ack 1 win 65535

// ESTABLISHED

  +.1 write(3, ..., 4000) = 4000
   +0 > .     1:1001(1000) ack 1
   +0 > P. 1001:2001(1000) ack 1
   +0 > .  2001:3001(1000) ack 1
   +0 > P. 3001:4001(1000) ack 1
 +.01 < .  1:1(0) ack 2001 win 49000
 +.01 < .  1:1(0) ack 4001 win 50000
